plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    id 'kotlin-kapt'
    alias(libs.plugins.hilt.android)
    alias(libs.plugins.secrets)
}

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionOffset = 0

android {
    compileSdk libs.versions.compileSdk.get().toInteger()
    buildToolsVersion = libs.versions.buildTools.get()

    defaultConfig {
        applicationId "com.github.watabee.qiitacompose"
        minSdkVersion libs.versions.minSdk.get().toInteger()
        targetSdkVersion libs.versions.targetSdk.get().toInteger()
        versionCode ((versionMajor * 10000 + versionMinor * 100 + versionPatch) * 100 + versionOffset)
        versionName "$versionMajor.$versionMinor.$versionPatch"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            storeFile rootProject.file("config/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.debug // TODO

            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildFeatures {
        buildConfig true
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion libs.versions.compose.compiler.get()
    }

    packagingOptions {
        exclude("META-INF/*.kotlin_module")
    }
    namespace 'com.github.watabee.qiitacompose'
}

dependencies {
    implementation projects.data
    implementation projects.ui.common
    implementation projects.ui.home
    implementation projects.ui.login
    implementation projects.ui.user
    implementation projects.ui.mypage
    implementation projects.ui.items
    implementation projects.ui.search

    implementation libs.coroutines.core
    implementation libs.coroutines.android

    implementation libs.androidx.appcompat
    implementation libs.androidx.activity.ktx
    implementation libs.androidx.core.ktx

    implementation libs.androidx.lifecycle.viewmodel.ktx

    implementation libs.material

    // Compose
    implementation platform(libs.androidx.compose.bom)
    implementation libs.androidx.compose.runtime
    implementation libs.androidx.compose.ui
    implementation libs.androidx.compose.foundation
    implementation libs.androidx.compose.foundation.layout
    implementation libs.androidx.compose.material
    implementation libs.androidx.compose.ui.tooling
    implementation libs.androidx.lifecycle.viewmodel.compose
    implementation libs.androidx.navigation.compose

    implementation libs.coil
    implementation libs.accompanist.insets

    implementation libs.hilt.android
    kapt libs.hilt.android.compiler

    implementation libs.timber
}

hilt {
    enableAggregatingTask = true
}
