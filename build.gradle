// Top-level build file where you can add configuration options common to all sub-projects/modules.
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    alias(libs.plugins.android.application).apply(false)
    alias(libs.plugins.android.library).apply(false)
    alias(libs.plugins.compose.compiler).apply(false)
    alias(libs.plugins.kotlin.jvm).apply(false)
    alias(libs.plugins.kotlin.android).apply(false)
    alias(libs.plugins.ksp).apply(false)
    alias(libs.plugins.spotless).apply(false)
    alias(libs.plugins.secrets).apply(false)
    alias(libs.plugins.hilt.android).apply(false)
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()

        // Used for Accompanist snapshots
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    }

    tasks.withType(KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = "11"
            freeCompilerArgs += ["-opt-in=kotlin.RequiresOptIn"]
        }
    }
}

subprojects { subproject ->
    pluginManager.apply(libs.plugins.spotless.get().pluginId)

    spotless {
        kotlin {
            target '**/*.kt'
            targetExclude("$buildDir/**/*.kt")
            targetExclude('bin/**/*.kt')

            ktlint(libs.versions.ktlint.get())
                .editorConfigOverride(
                    [
                        "disabled_rules": "filename"
                    ]
                )
        }
    }

    afterEvaluate {
        tasks.withType(KotlinCompile).configureEach {
            it.dependsOn spotlessApply
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
